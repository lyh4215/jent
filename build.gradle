plugins {
    id 'groovy'
    id 'jacoco'
}

group = 'org.jent'
version = '0.1.0'

repositories {
    mavenCentral()
    maven { url 'https://repo.jenkins-ci.org/public/' }
}

dependencies {
    implementation localGroovy()
    compileOnly 'com.cloudbees:groovy-cps:4014.vcd7dc51d8b_30'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.lesfurets:jenkins-pipeline-unit:1.1'
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src']
        }
    }
    test {
        groovy {
            srcDirs = ['test/groovy']
        }
    }
}

jacoco {
    toolVersion = '0.8.12'
}

tasks.named('test') {
    finalizedBy tasks.named('jacocoTestReport'), tasks.named('jacocoTestCoverageVerification')
}

tasks.named('jacocoTestReport') {
    dependsOn tasks.named('test')
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
}

tasks.named('jacocoTestCoverageVerification') {
    dependsOn tasks.named('test')
    violationRules {
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.60
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.45
            }
        }
    }
}

tasks.named('check') {
    dependsOn tasks.named('jacocoTestCoverageVerification')
}
